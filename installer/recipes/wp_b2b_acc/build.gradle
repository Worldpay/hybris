// Hybris b2b_acc recipe extended with Worldpay addon
apply plugin: 'installer-platform-plugin'
apply plugin: 'installer-addon-plugin'

def config = {
    localProperties {
        property 'googleApiKey', ''
        property 'website.powertools.http', 'http://powertools.local:9001/yb2bacceleratorstorefront'
        property 'website.powertools.https', 'https://powertools.local:9002/yb2bacceleratorstorefront'
        property 'apiregistryservices.events.exporting', 'false'
        property 'ansi.colors', 'true'
        property 'testclasses.addonname', 'yb2bacceleratorstorefront'
    }

    extensions {
        scanPathWithAutoLoad '${HYBRIS_BIN_DIR}/y-ext/ext-worldpayfulfilment'

        extName 'adaptivesearchsolr'
        extName 'adaptivesearchbackoffice'
        extName 'adaptivesearchsamplesaddon'
        extName 'adaptivesearchwebservices'
        extName 'b2bcommercebackoffice'
        extName 'solrfacetsearchbackoffice'

        extName 'yacceleratorbackoffice'
        extName 'yacceleratorinitialdata'
        extName 'yacceleratorfulfilmentprocess'
        extName 'ycommercewebservices'
        extName 'ycommercewebservicestest'
        extName 'orderselfserviceaddon'

        extName 'powertoolsstore'
        extName 'solrserver'

        extName 'acceleratorwebservicesaddon'
        extName 'b2bacceleratoraddon'
        extName 'commerceorgaddon'
        extName 'commerceorgsamplesaddon'

        extName 'customersupportbackoffice'
        extName 'customerticketingaddon'

        extName 'rulebuilderbackoffice'
        extName 'couponbackoffice'
        extName 'droolsruleengineservices'
        extName 'couponfacades'
        extName 'couponservices'
        extName 'promotionenginesamplesaddon'

        extName 'textfieldconfiguratortemplatebackoffice'
        extName 'textfieldconfiguratortemplateaddon'

        extName 'cmswebservices'
        extName 'smarteditwebservices'
        extName 'cmssmarteditwebservices'
        extName 'permissionswebservices'
        extName 'smarteditaddon'
        extName 'cmssmartedit'
        extName 'cmsbackoffice'
        extName 'assistedservicestorefront'
        extName 'assistedservicewebservices'
        extName 'cmsoccaddon'

        extName 'previewpersonalizationweb'
        extName 'personalizationcmsweb'
        extName 'personalizationsmartedit'
        extName 'personalizationpromotionssmartedit'
        extName 'personalizationsampledataaddon'
        extName 'personalizationpromotionssampledataaddon'
        extName 'personalizationpromotions'
        extName 'personalizationpromotionsweb'
        extName 'personalizationservicesbackoffice'
        extName 'personalizationpromotionsbackoffice'
        extName 'personalizationcmsbackoffice'
        extName 'personalizationsearchbackoffice'
        extName 'personalizationsearchsmartedit'
        extName 'personalizationsearchweb'
        extName 'personalizationsearchsamplesaddon'

        extName 'pcmbackofficesamplesaddon'

        /* integration-apis */
        extName 'outboundservices'
        extName 'odata2webservices'
        extName 'odata2webservicesfeaturetests'

        extName 'kymaintegrationbackoffice'
        extName 'kymaintegrationsampledata'

        extName 'worldpayaddonbackoffice'
        extName 'worldpayapi'
        extName 'worldpayb2baddon'
        extName 'worldpaynotifications'
        extName 'worldpaysampledataaddon'
        extName 'worldpayhac'
    }
}

def addExtgen = {
    println "Adding empty extgen.properties for yb2bacceleratorstorefront."
    def customFolder = new File("${project.rootDir}/../hybris/bin/custom")
    def targetFile = new File("${project.rootDir}/../hybris/bin/custom/yb2bacceleratorstorefront/extgen.properties")
    if (customFolder.exists() && !targetFile.exists()) {
        targetFile.write "# Empty extgen for compliance with ExtensionRequirementsTest"
        println "Finished adding extgen.properties for yb2bacceleratorstorefront."
    } else {
        println "Unable to add extgen.properties for yb2bacceleratorstorefront."
    }
}

task setup {
    doLast {
        def pl = platform(config)
        pl.setup()

        pl.executeAntTarget 'extgen -Dhybris.target=extgen -Dinput.template=yacceleratorstorefront -Dinput.name=yb2bacceleratorstorefront -Dinput.package=de.hybris.platform.yb2bacceleratorstorefront'

        pl.extensions {
            extName 'yb2bacceleratorstorefront'
        }
        pl.setup()

        pl.project.addons {
            names "worldpayb2baddon,worldpaysampledataaddon,b2bacceleratoraddon,commerceorgaddon,commerceorgsamplesaddon,promotionenginesamplesaddon,textfieldconfiguratortemplateaddon,smarteditaddon," +
                    "assistedservicestorefront,customerticketingaddon,orderselfserviceaddon,adaptivesearchsamplesaddon,pcmbackofficesamplesaddon,personalizationsearchsamplesaddon"
            template "yacceleratorstorefront"
            storeFronts "yb2bacceleratorstorefront"
            platform pl
        }

        pl.project.addons {
            names "acceleratorwebservicesaddon,cmsoccaddon"
            template "ycommercewebservices"
            storeFronts "ycommercewebservices"
            platform pl
        }

        addExtgen()
    }
}

task initialize {
    doLast {
        def pl = platform(config)
        pl.build()
        pl.initialize()
    }
}


task start {
    doLast {
        def pl = platform(config)
        pl.start()
    }
}
